import java.io.ByteArrayOutputStream

plugins {
    id("net.fabricmc.fabric-loom")
    kotlin("jvm") version "2.3.20"
    alias(libs.plugins.ksp)
    alias(libs.plugins.meowdding.auto.mixins)
    alias(libs.plugins.buildconfig)
    `versioned-catalogues`
    idea
}

val archiveName = "iconographic"

group = "me.owdding"
version = "1.0.0"

repositories {
    fun scopedMaven(url: String, vararg paths: String) = maven(url) { content { paths.forEach(::includeGroupAndSubgroups) } }

    scopedMaven("https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1", "me.djtheredstoner")
    scopedMaven("https://repo.hypixel.net/repository/Hypixel", "net.hypixel")
    scopedMaven("https://maven.parchmentmc.org/", "org.parchmentmc")
    scopedMaven("https://api.modrinth.com/maven", "maven.modrinth")
    scopedMaven(
        "https://maven.teamresourceful.com/repository/maven-public/",
        "earth.terrarium",
        "com.teamresourceful",
        "tech.thatgravyboat",
        "me.owdding",
        "com.terraformersmc"
    )
    scopedMaven("https://maven.nucleoid.xyz/", "eu.pb4")
    mavenCentral()
}


kotlin {
    jvmToolchain(25)
    compilerOptions {
        freeCompilerArgs.add("-Xname-based-destructuring=complete")
        freeCompilerArgs.add("-Xcontext-parameters")
        freeCompilerArgs.add("-Xnullability-annotations=@org.jspecify.annotations:warn")
    }
}


val gitRef = tasks.register<Exec>("gitRef") {
    outputs.upToDateWhen { false }
    standardOutput = ByteArrayOutputStream()
    commandLine("git", "rev-parse", "HEAD")
}

val gitBranch = tasks.register<Exec>("getBranch") {
    outputs.upToDateWhen { false }
    standardOutput = ByteArrayOutputStream()
    commandLine("git", "rev-parse", "--abbrev-ref", "HEAD")
}


val lastVersionBump = tasks.register<Exec>("getGradlePropertiesBump") {
    outputs.upToDateWhen { false }
    standardOutput = ByteArrayOutputStream()
    commandLine("git", "log", "-1", "--pretty=format:\"%H\"", "--", "gradle.properties")
}

val commitHash = tasks.register("commitHash") {
    outputs.upToDateWhen { false }
    this.mustRunAfter(lastVersionBump, gitRef)
    this.dependsOn(lastVersionBump, gitRef)
    doLast {
        val lastCommit = gitRef.get().standardOutput.toString()
        val lastVersionBump = lastVersionBump.get().standardOutput.toString()

        ext.set(
            "commitHash", if (lastVersionBump.equals(lastCommit, ignoreCase = true)) ""  else lastCommit.take(8)
        )
    }
}

tasks.processResources {
    dependsOn(commitHash)
    mustRunAfter(commitHash)
    val range = if ("minecraft.range" in versionedCatalog.versions) {
        versionedCatalog.versions["minecraft.range"].toString()
    } else {
        val start = versionedCatalog.versions.getOrFallback("minecraft.start", "minecraft")
        val end = versionedCatalog.versions.getOrFallback("minecraft.end", "minecraft")
        ">=$start <=$end"
    }
    val replacements = mapOf(
        "version" to project.provider {
            val commitHash = ext.get("commitHash")?.let { "+$it" } ?: ""
            "$version$commitHash"
        },
        "minecraft_range" to range,
        "fabric_lang_kotlin" to versionedCatalog.versions["fabric.language.kotlin"],
        "sbapi" to versionedCatalog.versions["skyblockapi"],
        "rconfigkt" to versionedCatalog.versions["resourceful.config.kotlin"],
        "rconfig" to versionedCatalog.versions["resourceful.config"],
    )
    outputs.upToDateWhen { false }

    filesMatching("fabric.mod.json") {
        expand(replacements.map {
            it.key to when (val v = it.value) {
                is Provider<*> -> v.get()
                else -> v
            }
        }.toMap())
    }
    with(copySpec {
        from(rootProject.file("src/lang")).include("*.json").into("assets/iconographic/lang")
    })

    filesMatching("**/*.kts") {
        exclude()
    }
}

dependencies {
    minecraft(versionedCatalog["minecraft"])

    implementation(versionedCatalog["fabric.api"])
    implementation(libs.fabric.loader)
    implementation(libs.fabric.language.kotlin)

    implementation(libs.meowdding.ktmodules)
    ksp(libs.meowdding.ktmodules)

    "api"(versionedCatalog["skyblockapi"]) {
        capabilities { requireCapability("tech.thatgravyboat:skyblock-api-${stonecutter.current.version}") }
    }
    "include"(versionedCatalog["skyblockapi"]) {
        capabilities { requireCapability("tech.thatgravyboat:skyblock-api-${stonecutter.current.version}") }
    }
    "api"(versionedCatalog["meowdding.lib"]) {
        capabilities { requireCapability("me.owdding.meowdding-lib:meowdding-lib-${stonecutter.current.version}") }
    }
    "include"(versionedCatalog["meowdding.lib"]) {
        capabilities { requireCapability("me.owdding.meowdding-lib:meowdding-lib-${stonecutter.current.version}") }
    }

    implementation(versionedCatalog["resourceful.config"])
    include(versionedCatalog["resourceful.config"])
    implementation(versionedCatalog["resourceful.config.kotlin"])
    include(versionedCatalog["resourceful.config.kotlin"])
    implementation(versionedCatalog["placeholders"])

    implementation(versionedCatalog["olympus"])
    include(versionedCatalog["olympus"])
    implementation(versionedCatalog["resourceful.lib"])
    include(versionedCatalog["resourceful.lib"])
}

base {
    archivesName = archiveName
}

tasks.build {
    this.dependsOn(commitHash)
    this.mustRunAfter(commitHash)
    doLast {
        val commitHash = ext.get("commitHash")?.let { "+$it" } ?: ""
        val sourceFile = rootProject.projectDir.resolve("versions/${project.name}/build/libs/${archiveName}-$version.jar")
        val targetFile = rootProject.projectDir.resolve("build/libs/${archiveName}-$version-${stonecutter.current.version}$commitHash.jar")
        targetFile.parentFile.mkdirs()
        targetFile.writeBytes(sourceFile.readBytes())
    }
}

autoMixins {
    mixinPackage = "me.owdding.iconographic.mixins"
    projectName = "iconographic"
}

ksp {
    arg("meowdding.project_name", "Iconographic")
    arg("meowdding.package", "me.owdding.iconographic.generated")
}

loom {
    runConfigs["client"].apply {
        ideConfigGenerated(true)
        runDir = "../../run"
        vmArg("-Dfabric.modsFolder=${rootProject.projectDir.resolve("run/${stonecutter.current.version}Mods").absolutePath}")
    }

    accessWidenerPath = rootProject.file("src/main/resources/iconographic.accesswidener")
}


tasks.generateBuildConfig {
    dependsOn(gitRef, gitBranch)
    mustRunAfter(gitRef, gitBranch)
}

buildConfig {
    packageName("me.owdding.iconographic.generated")
    className("BuildInfo")

    buildConfigField("String", "VERSION", "\"${rootProject.version}\"")

    buildConfigField("String", "GIT_BRANCH", gitBranch.map { "\"${it.standardOutput.toString().substringBefore("\n")}\"" })
    buildConfigField("String", "GIT_REF", gitRef.map { "\"${it.standardOutput.toString().substringBefore("\n")}\"" })
}
